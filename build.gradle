// Default tasks
defaultTasks 'clean', 'build', 'licenseFormatMain', 'shadow'

// Apply plugins
apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'shadow'
apply plugin: 'license'

// Basic project information
group = 'org.spoutcraft'
archivesBaseName = 'client'
version = '1.0.0-SNAPSHOT'
mainClassName = "org.spoutcraft.client.Main"

// Extended project information
ext.projectName = 'Client'
ext.inceptionYear = '2013'
ext.packaging = 'jar'
ext.url = 'http://spoutcraft.org'
ext.description = 'Open source, multi-threaded Minecraft client written in Java.'
ext.organization = 'Spoutcraft'

// Minimum version of Java required
sourceCompatibility = '1.7'
targetCompatibility = '1.7'

// Define variables
ext.buildNumber = '0'
ext.ciSystem = 'unknown'
ext.commit = 'unknown'

// Plugin repositories and dependencies
buildscript {
    repositories {
        mavenCentral()
        maven {
            name = 'sonatype-nexus-public'
            url = 'https://oss.sonatype.org/content/repositories/public/'
        }
        maven {
            name = 'shadow-repo'
            url = 'http://dl.bintray.com/content/johnrengelman/gradle-plugins/'
        }
    }
    dependencies {
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.6.0'
        classpath 'org.gradle.plugins:shadow:0.7.4'
    }
}

// Non-plugin repositories and dependencies
repositories {
    mavenCentral()
    maven {
        name = 'sonatype-nexus-public'
        url = 'https://oss.sonatype.org/content/repositories/public/'
    }
}
dependencies {
    compile 'com.flowpowered:flow-networking:0.1.0-SNAPSHOT'
    compile 'org.spout:caustic:1.0.0-SNAPSHOT'
    compile 'org.spout:react:1.0.0-SNAPSHOT'
    compile 'org.spout:spout-math:1.0.0-SNAPSHOT'
    compile 'commons-io:commons-io:2.4'
    compile 'org.apache.commons:commons-lang3:3.1'
}

// Filter, process, and include resources
processResources {
    from(rootProject.rootDir) {
        include 'LICENSE.txt'
    }
}

// Include dependencies in final JAR
shadow {
    artifactAttached = false
    include 'com/flowpowered/**'
    include 'org/spout/**'
    include 'org/apache/**'
}

// License header formatting
license {
    ext.name = projectName
    ext.organization = organization
    ext.url = url
    ext.year = inceptionYear
    header rootProject.file('HEADER.txt')
    strictCheck true
}

// Source compiler configuration
configure([compileJava, compileTestJava]) {
    options.compilerArgs << '-Xlint:all'
    options.compilerArgs << '-Xlint:-path'
    options.deprecation = true
}

// JAR manifest configuration
manifest.mainAttributes(
        "Main-Class": mainClassName,
        "Built-By": System.properties['user.name'],
        "Created-By": System.properties['java.vm.version'] + " (" + System.properties['java.vm.vendor'] + ")",
        "Implementation-Title": name,
        "Implementation-Version": version + "+" + ciSystem + "-b" + buildNumber + ".git-" + commit,
        "Implementation-Vendor": url)

